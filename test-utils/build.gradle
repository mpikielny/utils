sourceSets {
    guice {
        java.srcDir 'src/guice/java'
    }
    logback {
        java.srcDir 'src/logback/java'
    }
    log4j {
        java.srcDir 'src/log4j/java'
    }
}

dependencies {
    implementation libs.junit, libs.mockito
    implementation project(':stream-utils')
    implementation project(':reflect-utils')
    implementation project(':generic-utils')
    implementation libs.slf4j
    implementation libs.guava
    compileOnly libs.autoService
    annotationProcessor libs.autoService

    logbackImplementation libs.slf4j, libs.logbackClassic
    logbackCompileOnly libs.autoService
    logbackAnnotationProcessor libs.autoService
    logbackImplementation project.sourceSetDependency(sourceSets.main)

    log4jImplementation libs.slf4j, libs.log4jApi, libs.log4jCore, libs.log4jSlf4jBridge
    log4jCompileOnly libs.autoService
    log4jAnnotationProcessor libs.autoService
    log4jImplementation project.sourceSetDependency(sourceSets.main)

    guiceImplementation project.sourceSetDependency(sourceSets.main)
    guiceImplementation project.sourceSetDependency(project(':guice-utils').sourceSets.main)
    guiceImplementation libs.guice
    guiceImplementation libs.junit, libs.mockito
    guiceImplementation project(':stream-utils')
    guiceImplementation project(':generic-utils')

    testImplementation libs.guice
    testImplementation project.sourceSetDependency(sourceSets.guice)
    testImplementation project.sourceSetDependency(sourceSets.logback)
    testImplementation project.sourceSetDependency(sourceSets.log4j)
}

configurations {
    guiceOutput
    guiceImplementation.extendsFrom implementation
    logbackImplementation.extendsFrom implementation
    log4jImplementation.extendsFrom implementation
    testImplementation.extendsFrom guiceImplementation, logbackImplementation, log4jImplementation
}

task guiceOutputJar(type: Jar) {
    from sourceSets.guice.output
}

artifacts {
    guiceOutput guiceOutputJar
}
